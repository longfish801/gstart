/*
 * build.gradle
 *
 * Copyright (C) io.github.longfish801 All Rights Reserved.
 */

plugins { id 'edu.sc.seis.launch4j' version '2.3.0' }

version = '0.2.00';

dependencies {
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3';
	compile group: 'io.github.longfish801', name: 'clmap', version: '0.2.00';
	compile group: 'io.github.longfish801', name: 'shared', version: '0.2.00';
	compile group: 'io.github.longfish801', name: 'yakumo', version: '0.2.00';
	compile group: 'org.apache.ivy', name: 'ivy', version: '2.4.0';
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1';
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.3';
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25';
	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.5';
}

jar {
	manifest.attributes(
		'Main-Class': 'io.github.longfish801.gstart.Gstart',
		'Class-Path': "${configurations.runtime.files.collect { it.name }.join(' ')} ../conf/"
	)
}

// 実行ファイル作成タスクです。
createExe {
	jar = "lib/${tasks.jar.outputs.files[0].name}";
	outfile = "${project.name}.exe";
	icon = '../resources/main/icon.ico';
	dontWrapJar = true;
	language = 'JAPANESE';
//	headerType = 'console';
}

// デプロイ関連のタスクです。
task deployDelete(type: Delete){
	delete 'deploy/conf/version.txt';
	delete 'deploy/lib';
	delete 'deploy/log/app.log';
	delete "deploy/${project.name}.exe"
}
task deploy(type: Copy, dependsOn: [createExe, deployDelete]){
	from 'build/launch4j';
	into 'deploy';
}
deploy.doLast {
	file('deploy/conf/version.txt').setText("gstart version ${version}");
}
clean.dependsOn deployDelete;

