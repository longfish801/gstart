/*
 * build.gradle
 *
 * Copyright (C) io.github.longfish801 All Rights Reserved.
 */

plugins { id 'edu.sc.seis.launch4j' version '2.3.0' }

version = '0.1.00';

dependencies {
	compile 'ch.qos.logback:logback-classic:1.0.12';
	compile 'io.github.longfish801:shared:0.1.00';
	compile 'io.github.longfish801:yakumo:0.1.00';
	compile 'org.apache.commons:commons-lang3:3.3.2';
	compile 'org.apache.ivy:ivy:2.4.0';
	compile 'org.codehaus.groovy:groovy-all:2.4.11';
	compile 'org.slf4j:slf4j-api:1.7.5';
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4';
}

jar {
	manifest.attributes(
		'Main-Class': 'io.github.longfish801.gstart.Gstart',
		'Class-Path': "${configurations.runtime.files.collect { it.name }.join(' ')} ../conf/"
	)
}

// 実行ファイル作成タスクです。
createExe {
	jar = "lib/${tasks.jar.outputs.files[0].name}";
	outfile = "${project.name}.exe";
	icon = '../resources/main/icon.ico';
	dontWrapJar = true;
	language = 'JAPANESE';
//	headerType = 'console';
}

// リリース関連のタスクです。
task releaseDelete(type: Delete){
	delete 'release/conf/version.txt';
	delete 'release/lib';
	delete 'release/log/app.log';
	delete "release/${project.name}.exe"
}
task release(type: Copy, dependsOn: [createExe, releaseDelete]){
	from 'build/launch4j';
	into 'release';
}
release.doLast {
	file('release/conf/version.txt').setText("gstart version ${version}");
}
clean.dependsOn releaseDelete;

